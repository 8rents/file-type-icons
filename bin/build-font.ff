#!/usr/bin/env fontforge

name    = "File Icons"
font    = "file-icons.ttf"
version = "2.1.47"
author  = "John Gardner"
url     = "https://github.com/Alhadis"
license = "http://opensource.org/licenses/ISC"
tableID = "fi"
fond    = "Ficon"
font    = $0:h + "/../" + font
table   = $0:h + "/../icons.tsv"
svgDir  = $0:h + "/../svg/"
em      = 1024
base    = Round(em * 0.0625)
mono    = 0

# Copyright info
notice = LoadStringFromFile($0:h + "/../LICENSE.md")
notice = Strsub(notice, 0, Strstr(notice, Chr(0x0A)))

# Style
if(mono) style = "Monospace"; fond += "Fix"
else     style = "Regular";   fond += "Reg"
endif

# PostScript name
psName = ""
i = 0
while(i < Strlen(name))
	code = Ord(name, i++)
	char = Chr(code)
	if(code >= 33 && code <= 126 && !~Strstr("[](){}<>/%", char))
		psName += char
	endif
endloop
psName += "-" + style


# Begin font construction
New()
Reencode("unicode4")
ScaleToEm(em - base, base)
SetTTFName(0x409, 0, notice)
SetTTFName(0x409, 1, name)
SetTTFName(0x409, 2, style)
SetTTFName(0x409, 3, name)
SetTTFName(0x409, 4, name)
SetTTFName(0x409, 5, "Version " + version)
SetTTFName(0x409, 6, psName)
SetTTFName(0x409, 9, author)
SetTTFName(0x409, 12, url)
SetTTFName(0x409, 14, license)
SetPanose([5, 10, 1, 2 - -mono, 1, 2, 2, 2, 2, 2])
SetMacStyle(0x00)
SetFondName(fond)

PI = 3.141592653589793
xo = Sin($italicangle * PI / 180)
SetOS2Value("Weight",        400)
SetOS2Value("Width",         5)
SetOS2Value("FSType",        0)
SetOS2Value("IBMFamily",     0x0F0F)
SetOS2Value("SubXSize",      Int(0.65 * $em))
SetOS2Value("SupXSize",      Int(0.65 * $em))
SetOS2Value("SubYSize",      Int(0.7  * $em))
SetOS2Value("SupYSize",      Int(0.7  * $em))
SetOS2Value("SubXOffset",    Int(0.14 * $em * -xo))
SetOS2Value("SubYOffset",    Int(0.14 * $em))
SetOS2Value("SupXOffset",    Int(0.48 * $em * xo))
SetOS2Value("SupYOffset",    Int(0.48 * $em))
SetOS2Value("StrikeOutPos",  Int(530  * $em / 2048))
SetOS2Value("StrikeOutSize", Int(102  * $em / 2048))
SetOS2Value("HHeadLineGap",  0)
SetOS2Value("VHeadLineGap",  0)
SetOS2Value("TypoLineGap",   $descent)
SetOS2Value("TypoAscent",    $ascent);  SetOS2Value("TypoAscentIsOffset",   0)
SetOS2Value("TypoDescent",  -$descent); SetOS2Value("TypoDescentIsOffset",  0)
SetOS2Value("HHeadAscent",   $ascent);  SetOS2Value("HHeadAscentIsOffset",  0)
SetOS2Value("HHeadDescent", -$descent); SetOS2Value("HHeadDescentIsOffset", 0)
SetOS2Value("WinAscent",     $ascent);  SetOS2Value("WinAscentIsOffset",    0)
SetOS2Value("WinDescent",    $descent); SetOS2Value("WinDescentIsOffset",   0)

table = ($0:h + "/load-table.ff")(table);
l = SizeOf(table)
i = 1
while(i < l)
	row  = table[i]
	
	if(row[_FONT_ID] == tableID)
		svgFile = svgDir + row[_FILE]
		Print("Importing: " + svgFile)
		Select(row[_CODE])
		Import(svgFile, 0)

		gvwidth = GlyphInfo("VWidth")
		gwidth  = GlyphInfo("Width")
		bbox    = GlyphInfo("BBox")
		width   = bbox[2] - bbox[0]
		height  = bbox[3] - bbox[1]
		if(width < height)
			Scale($em / height * 100)
		else
			Scale($em / width * 100)
		endif
		SetVWidth(gvwidth)
		SetWidth(gwidth)

		bbox = GlyphInfo("BBox")
		Move(-bbox[0], -bbox[1] - $descent / 2)
		if(mono)
			CenterInWidth()
		else
			SetWidth(Ceil(bbox[2] - bbox[0]))
		endif
		RoundToInt()
	endif
	++i
endloop

# Define a `.notdef` glyph to prevent FontForge from auto-generating one
Select(0u0000)
SetWidth($em)
SetGlyphName(".notdef")
DetachGlyphs()

SelectWorthOutputting()
Generate(font)
Close()
